import org.yaml.snakeyaml.Yaml

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.yaml:snakeyaml:1.17'
        classpath 'org.flywaydb:flyway-gradle-plugin:4.0.3'
    }
}

group 'com.nexters.shootingstar'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'org.flywaydb.flyway'

sourceCompatibility = 1.8

mainClassName = 'com.nexters.shootingstar.TodoApplication'

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile 'org.assertj:assertj-core:3.6.2'

    compile group: 'io.dropwizard', name: 'dropwizard-auth', version: '1.0.5'
    compile group: 'io.dropwizard', name: 'dropwizard-core', version: '1.0.5'
    compile group: 'io.dropwizard', name: 'dropwizard-jdbi', version: '1.0.5'
    compile group: 'io.dropwizard', name: 'dropwizard-hibernate', version: '1.0.5'
    compile 'org.apache.httpcomponents:httpclient:4.5.3'
    compile 'org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2:2.26-b02'
    compile group: 'io.dropwizard', name: 'dropwizard-testing', version: '1.0.5'
    // compile group: 'com.google.inject.extensions', name: 'guice-persist', version: '4.0'
    compile group: 'postgresql', name: 'postgresql', version: '9.0-801.jdbc4'
    compile 'org.projectlombok:lombok:1.16.10'
}

def yaml = new Yaml().load(project.file("src/main/resources/config/production.yml").newReader())

flyway {
    driver = yaml.database.driver
    url = yaml.database.url
    user = yaml.database.user
    password = yaml.database.password
}

jar {
    manifest {
        attributes "Main-Class": "$mainClassName"
    }
}

task fatJar(type: Jar) {
    manifest.from jar.manifest
    classifier = 'all'
    from {
        configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}